// Check if file containing signing config exists
def propertiesFile = rootProject.file("local.properties")
if (!propertiesFile.exists()) {
    logger.warn("File '${propertiesFile.name}' not found. Skipping release signing setup...")
    return
}

// Load properties
def properties = new Properties()
properties.load(new FileInputStream(propertiesFile))

// Check if release signing is enabled
if (!properties.getProperty("isReleaseSigningEnabled", "false").toBoolean()) {
    logger.info("Release signing is disabled. Skipping setup...")
    return
}

// Setup release build signing
android {
    signingConfigs {
        release {
            storeFile file(properties.getProperty("storeFile"))
            storePassword properties.getProperty("storePassword")
            keyAlias properties.getProperty("keyAlias")
            keyPassword properties.getProperty("keyPassword")
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }
}